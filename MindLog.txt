///==============================================================
/// MindLog v0.0.4
///==============================================================
/*
Author: CactusHand

Description: 
  A program to log information from
  the MindStone while playing through a run.
  Seems simple enough!

Features:
- A sizable console for text logging
- Buttons for scrolling through logs
- A button to hide / fold the console
- print() and println()

To Use:

var log = import (parent folder)/MindLog
log.lineLimit = 10       //optional
log.characterLimit = 30  //optional
log.Run()

?time = 1
  log.println(＂Hello World!＂)

*/
///==============================================================
/// Variables
///==============================================================

//--User Changable
var lineLimit      = 5  //between 2-20
var characterLimit = 30 //between 4-60

//--Internal
var drag = import UI/DragController
var textWindow = null
var scrollUp = null
var scrollDown = null
var scrollLock = null
var toggleButton = null

var textLog        = ［＂＂］
var scrollIndex    = 0
var isScrollLocked = false
var isHidden       = false

///==============================================================
/// Public Functions
///==============================================================

func print(message)
  var lastIndex = textLog.Count()-1
  
  textLog［lastIndex］ += message
  var line = textLog［lastIndex］
  
  ?string.Size(line) > characterLimit
  
    var newLine = string.Sub(
    ^line,characterLimit)
        
    textLog［lastIndex］ = string.Sub(
    ^line,0,characterLimit)
    
    println(newLine)
 //

func println(message)
  textLog.Add(＂＂)
  print(message)
//

///==============================================================
/// Private Functions
///==============================================================

func InitWindow()

  textWindow = ui.AddPanel()
  textWindow.anchor = top_left
  textWindow.dock   = top_left
  textWindow.w      = characterLimit+4
  textWindow.h      = lineLimit+2
  textWindow.x      = 0
  textWindow.y      = 0
  
  drag.Add(textWindow)
//

func LockPressed()
  ?isScrollLocked
    scrollUp.visible = false
    scrollDown.visible = false
    toggleButton.visible = true
    scrollLock.text = ＂||＂
    isScrollLocked = false
  :
    scrollUp.visible = true
    scrollDown.visible = true
    toggleButton.visible = false
    scrollLock.text = ＂<>＂
    isScrollLocked = true
//

func UpPressed()
  ?scrollIndex > 0
    scrollIndex--
//

func DownPressed()
  ?scrollIndex < textLog.Count() - lineLimit
    scrollIndex++
//

func TogglePressed()
  ?isHidden
    scrollLock.visible = true
    textWindow.visible = true
    toggleButton.text = ＂X＂
    isHidden = false
  :
    scrollLock.visible = false
    textWindow.visible = false
    toggleButton.text = ＂+＂
    isHidden = true
//

func InitButtons()
  scrollUp = ui.AddButton()
  scrollUp.SetPressed(UpPressed)
  scrollUp.visible = false
  scrollUp.tcolor  = #00aa00
  scrollUp.text    = ＂A＂
  scrollUp.anchor  = top_left
  scrollUp.dock    = top_left
  scrollUp.w       = 1
  scrollUp.h       = 1
  
  scrollDown = ui.AddButton()
  scrollDown.SetPressed(DownPressed)
  scrollDown.visible = false
  scrollDown.tcolor  = #00aa00
  scrollDown.text    = ＂V＂
  scrollDown.anchor  = top_left
  scrollDown.dock    = top_left
  scrollDown.w       = 1
  scrollDown.h       = 1
  
  scrollLock = ui.AddButton()
  scrollLock.SetPressed(LockPressed)
  scrollLock.visible = true
  scrollLock.tcolor  = #ffffff
  scrollLock.bcolor  = #ee0000
  scrollLock.text    = ＂||＂
  scrollLock.anchor  = top_left
  scrollLock.dock    = top_left
  scrollLock.w       = 4
  scrollLock.h       = 1
  
  toggleButton = ui.AddButton()
  toggleButton.SetPressed(TogglePressed)
  toggleButton.visible = true
  toggleButton.tcolor  = #ffffff
  toggleButton.bcolor  = #ee0000
  toggleButton.text    = ＂X＂
  toggleButton.anchor  = top_left
  toggleButton.dock    = top_left
  toggleButton.w       = 3
  toggleButton.h       = 1
//

func DrawMindLog()
  InitWindow()
  InitButtons()
//

func UpdatePositions()
  scrollLock.x = textWindow.x+3
  scrollLock.y = textWindow.y-1
  
  scrollUp.x = textWindow.x
  scrollUp.y = textWindow.y+1
  
  scrollDown.x = textWindow.x
  scrollDown.y = textWindow.y+2
  
  toggleButton.x = textWindow.x
  toggleButton.y = textWindow.y-1
  
  ?isScrollLocked
    >`@textWindow.x+1@,@textWindow.y@,#00aa00,@scrollIndex@
//

func UpdateTextDisplay()
  var topIndex = textLog.Count() - lineLimit - 1
  ?topIndex < 0
    topIndex = 0
  :?isScrollLocked
    topIndex = scrollIndex
  :
    scrollIndex = topIndex

    
  for i = 0..lineLimit-1
    ?i < textLog.Count()
      >`@textWindow.x+1@,
      ^ @textWindow.y+1+i@,
      ^ @textLog［i+topIndex］@
//

func StateController()
  drag.Update()
  ?!isHidden
    UpdateTextDisplay()
  UpdatePositions()
//

var initialized
func Run()

  ?lineLimit < 2
    lineLimit = 2
  :?lineLimit > 20
    lineLimit = 20

  ?characterLimit < 4
    characterLimit = 4
  :?characterLimit > 60
    characterLimit = 60
  
  ?loc.begin | loc.loop
    initialized = 0
  ?!initialized
    DrawMindLog()
    initialized = 1
  :
    StateController()
//

///==============================================================
/// End of MindLog
///==============================================================
